if (LOGIN_COMPONENT_RUNALONE.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}


android {

    compileSdkVersion ANDROID_BUILD_SDK_VERSION.toInteger()
    defaultConfig {
        if (LOGIN_COMPONENT_RUNALONE.toBoolean()) {
            applicationId "com.anonyper.logincomponent"
        }

        minSdkVersion Integer.parseInt(ANDROID_BUILD_MIN_SDK_VERSION as String)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION as String)
        versionCode Integer.parseInt(ANDROID_BUILD_VER_CODE as String)
        versionName ANDROID_BUILD_VER_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
            if (LOGIN_COMPONENT_RUNALONE.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/manifest/AndroidManifest.xml'
            }

        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(":basecomponent")
//    implementation project(":minecomponent")
    implementation 'com.alibaba:arouter-api:1.5.0'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
}
